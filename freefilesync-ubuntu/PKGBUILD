# Maintainer: t-onoz <t [dot] onozuka [dot] 1990 [at] (gmail)>
pkgname=freefilesync-ubuntu
pkgver=9.9
pkgrel=1
pkgdesc="Backup software to synchronize files and folders"
arch=('x86_64')
depends=('gtk2' 'libsm')
url="http://www.freefilesync.org/"
license=('GPL3')
provides=('freefilesync')
conflicts=('freefilesync')

source=(
  "FreeFileSync_${pkgver}_Ubuntu_17.10_64-bit.tar.gz::https://www.freefilesync.org/download_redirect.php?file=FreeFileSync_${pkgver}_Ubuntu_17.10_64-bit.tar.gz"
  FreeFileSync.desktop
  ffsicon.png
  RealTimeSync.desktop
  rtsicon.png
)

sha256sums=('7986b4ddad50a8668673ae8ccc27e0b3409af9fb7a095435a1f7a89ad97707fe'
            'b381bb9dbda25c3c08a67f18072a2761abe34339ddf3318e1758eb7c349f1a3b'
            '31df3fa1f1310de14bbd379f891d4f8ed2df5b0d68913eb52c88b3be682933fb'
            '1502efdbf1638856a18ab9916e0431bf6a53471792cb2daa380345bac33f67c4'
            'f28042587dbe99cf5d6bef2c1be4b026488e418e4ba8332b3016d246b7053a4e')

DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -A Mozilla -o %o %u')

package() {
  _bindir="${pkgdir}"/usr/bin
  _appname="FreeFileSync"
  _appsharedir="${pkgdir}"/usr/share/${_appname}
  _docsharedir="${pkgdir}"/usr/share/doc/${_appname}

  mkdir -p "${_bindir}"
  cp "${srcdir}"/${_appname}/{FreeFile,RealTime}Sync "${_bindir}"

  mkdir -p "${_appsharedir}"
  cp -R "${srcdir}"/${_appname}/Languages/ \
    "${srcdir}"/${_appname}/Help/ \
    "${srcdir}"/${_appname}/ding.wav \
    "${srcdir}"/${_appname}/gong.wav \
    "${srcdir}"/${_appname}/harp.wav \
    "${srcdir}"/${_appname}/Resources.zip \
    "${_appsharedir}"

  mkdir -p ${_docsharedir}
  cp "${srcdir}"/${_appname}/CHANGELOG "${_docsharedir}"/changelog

  install -Dm644 "${srcdir}"/FreeFileSync.desktop $pkgdir/usr/share/applications/FreeFileSync.desktop
  install -Dm644 "${srcdir}"/ffsicon.png $pkgdir/usr/share/pixmaps/ffsicon.png
  install -Dm644 "${srcdir}"/RealTimeSync.desktop $pkgdir/usr/share/applications/RealTimeSync.desktop
  install -Dm644 "${srcdir}"/rtsicon.png $pkgdir/usr/share/pixmaps/rtsicon.png
}
# vim:set ts=2 sw=2 et: